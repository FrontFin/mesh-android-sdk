apply plugin: 'jacoco'

jacoco {
    toolVersion '0.8.10'
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def buildDir = layout.buildDirectory

def classDirs = fileTree("$buildDir/tmp/kotlin-classes/release")

def sourceDirs = files("$projectDir/src/main/java")

def executionDirs = files("$buildDir/jacoco/testReleaseUnitTest.exec")

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn 'testReleaseUnitTest'
    group = "Reporting"
    description = "Code coverage report for Android & Unit tests."

    reports {
        html.required = true
        xml.required = true
    }

    classDirectories.from(classDirs)
    sourceDirectories.from(sourceDirs)
    executionData.from(executionDirs)
}

tasks.register('jacocoCoverageVerification', JacocoCoverageVerification) {
    dependsOn 'testReleaseUnitTest'
    group = "Verification"
    description = "Code coverage verification for Android & Unit tests."

    violationRules {
        rule {
            limit {
                minimum = 0.3
            }
        }
    }

    classDirectories.from(classDirs)
    sourceDirectories.from(sourceDirs)
    executionData.from(executionDirs)
}
