apply plugin: 'jacoco'

jacoco {
    toolVersion '0.8.11'
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

def classDir = files("build/tmp/kotlin-classes/debug/com")
def sourceDir = files('src/main/java')
def execFile = files('build/jacoco/testDebugUnitTest.exec')

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn 'testDebugUnitTest'
    group = "Reporting"
    description = "Generation of code coverage report."
    reports {
        html.required = true
        xml.required = true
    }
    classDirectories = classDir
    sourceDirectories = sourceDir
    executionData = execFile
}

tasks.register('jacocoCoverageVerification', JacocoCoverageVerification) {
    dependsOn 'testDebugUnitTest'
    group = "Verification"
    description = "Verification of code coverage level."
    violationRules {
        rule {
            limit {
                minimum = 0.45
            }
        }
    }
    classDirectories = classDir
    sourceDirectories = sourceDir
    executionData = execFile
}
