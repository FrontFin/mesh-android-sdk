plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.jlleitschuh.ktlint)
    alias(libs.plugins.jetbrains.dokka)
    alias(libs.plugins.vanniktech.maven.publish)
    alias(libs.plugins.kotlin.parcelize)

}
apply from: "$rootDir/detekt.gradle"
apply from: 'jacoco.gradle'
apply from: 'sonar.gradle'
apply from: "secrets.gradle"
apply from: 'version.gradle'

android {
    namespace 'com.meshconnect.link'

    compileSdk 34
    defaultConfig {
        minSdk 24
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
        versionCode 1
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        buildConfigField "String", "VERSION", "\"$publishVersion\""
    }
    // see https://googlesamples.github.io/android-custom-lint-rules/user-guide.md.html#lintgradleplugindsl
    // and https://googlesamples.github.io/android-custom-lint-rules/user-guide.md.html#performancetuning
    lint {
        abortOnError true
        ignoreTestSources true
        warningsAsErrors true
    }
    kotlinOptions {
        freeCompilerArgs += [
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
        ]
    }
    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.google.material
    implementation libs.google.gson
    implementation libs.kotlinx.coroutines.core

    testImplementation libs.junit
    testImplementation libs.mockk
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.kluent
    testImplementation libs.androidx.core.testing

    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.kluent
}
/**
 * Publication setup to Maven Central
 */
import com.vanniktech.maven.publish.AndroidSingleVariantLibrary
import com.vanniktech.maven.publish.SonatypeHost

mavenPublishing {
    // the first parameter represents which variant is published
    // the second whether to publish a sources jar
    // the third whether to publish a javadoc jar
    configure(new AndroidSingleVariantLibrary('release', true, true))
    // the first parameter represents the repository to publish
    // the second whether to publish automatically
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, false)
    // sing publication
    signAllPublications()
    // define group, artifact and version
    // the version is taken from `link/version.gradle` file
    coordinates('com.meshconnect', 'link', publishVersion)

    pom {
        name = 'Mesh Connect Android SDK'
        description = 'Android library for integrating with Mesh Connect'
        url = 'https://github.com/FrontFin/mesh-android-sdk'

        scm {
            url = 'https://github.com/FrontFin/mesh-android-sdk'
            connection = 'scm:git:git://github.com/FrontFin/mesh-android-sdk.git'
            developerConnection = 'scm:git:ssh://git@github.com/FrontFin/mesh-android-sdk.git'
            // set commit hash as a tag to track the release source
            tag = 'git rev-parse --verify HEAD'.execute().text.trim()
        }
        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/licenses/MIT'
            }
        }
        developers {
            developer {
                id = 'meshconnect'
                name = 'Mesh Connect, Inc'
                url = 'https://github.com/FrontFin'
            }
        }
    }
}
